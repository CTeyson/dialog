import java.nio.file.Paths

group 'org.rewedigital.voice.dialog'
version rootProject.ext.versions.spring_plugin

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-allopen:${rootProject.ext.versions.kotlin}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'maven-publish'
apply from: '../docu.gradle'
apply plugin: 'com.novoda.bintray-release'

dependencies {
    implementation "org.rewedigital.voice:dialog:${rootProject.ext.versions.core}"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.springframework:spring-context:5.1.4.RELEASE"
}

dokka {
    externalDocumentationLink {
        url = new URL("https://github.com/rewe-digital-incubator/${rootProject.name}/blob/master/docs/core/")
        packageListUrl = Paths.get("$rootDir/docs/core/package-list").toUri().toURL()
    }
    externalDocumentationLink {
        url = new URL("https://docs.spring.io/spring-framework/docs/current/javadoc-api/")
        packageListUrl = new URL("https://docs.spring.io/spring-framework/docs/current/javadoc-api/package-list")
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier = 'javadoc'
    from "$buildDir/javadoc"
}

publishing.publications {
    springPlugin(MavenPublication) {
        groupId = 'org.rewedigital.voice.dialog'
        artifactId = 'spring-plugin'
        from components.java
        version = "${rootProject.ext.versions.core}"
        artifact sourcesJar {
            archiveClassifier = 'sources'
        }
        artifact javadocJar {
            archiveClassifier = 'javadoc'
        }
        pom {
            name = 'dialog'
            description = 'This is the Spring plugin from Dialog to write voice applications fast.'
            url = 'https://github.com/rewe-digital-incubator/dialog'
            licenses {
                license {
                    name = 'MIT License'
                    url = 'https://opensource.org/licenses/MIT'
                    distribution = 'repo'
                }
            }
            developers {
                // TODO use the MAINTAINERS file
                developer {
                    name = 'Volkmar Vogel'
                    email = 'volkmar.vogel@rewe-digital.com'
                }
                developer {
                    name = 'Ren√© Kilczan'
                    email = 'rene.kilczan@rewe-digital.com'
                }
            }

            scm {
                connection = 'scm:git:git://github.com/rewe-digital-incubator/dialog.git'
                developerConnection = 'scm:git:git://github.com/rewe-digital-incubator/dialog.git'
                url = 'https://github.com/rewe-digital-incubator/dialog'
            }
        }
    }
}

publish {
    userOrg = 'rewe-digital'
    groupId = 'org.rewedigital.voice'
    artifactId = 'spring-plugin'
    repoName = 'dialog'
    publishVersion = rootProject.ext.versions.core
    desc = 'Dialog is a Dialogflow v2 API implementation written in Kotlin. With some great optional extensions you can use to write your own voice application fast.'
    website = 'https://github.com/rewe-digital-incubator/dialog'
    licences = ['MIT']
    bintrayUser = project.properties['bintray.user']
    bintrayKey = project.properties['bintray.apikey']
    dryRun = false
    publications = ['springPlugin']
}
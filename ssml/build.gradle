group 'org.rewedigital.voice'
version rootProject.ext.versions.ssml

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply from: '../docu.gradle'
apply plugin: 'com.novoda.bintray-release'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier = 'javadoc'
    from "$buildDir/javadoc"
}

publishing.publications {
    ssmlBuilder(MavenPublication) {
        groupId = 'org.rewedigital.voice'
        artifactId = 'ssml-builder'
        from components.java
        version = "${rootProject.ext.versions.core}"
        artifact sourcesJar {
            archiveClassifier = 'sources'
        }
        artifact javadocJar {
            archiveClassifier = 'javadoc'
        }
        pom {
            name = 'dialog'
            description = 'This library helps you to creates Speech Synthesis Markup Language (SSML) for your voice applications.'
            url = 'https://github.com/rewe-digital-incubator/dialog'
            licenses {
                license {
                    name = 'MIT License'
                    url = 'https://opensource.org/licenses/MIT'
                    distribution = 'repo'
                }
            }
            developers {
                // TODO use the MAINTAINERS file
                developer {
                    name = 'Volkmar Vogel'
                    email = 'volkmar.vogel@rewe-digital.com'
                }
                developer {
                    name = 'Ren√© Kilczan'
                    email = 'rene.kilczan@rewe-digital.com'
                }
            }

            scm {
                connection = 'scm:git:git://github.com/rewe-digital-incubator/dialog.git'
                developerConnection = 'scm:git:git://github.com/rewe-digital-incubator/dialog.git'
                url = 'https://github.com/rewe-digital-incubator/dialog'
            }
        }
    }
}

publish {
    userOrg = 'rewe-digital'
    groupId = 'org.rewedigital.voice'
    artifactId = 'ssml-builder'
    repoName = 'dialog'
    publishVersion = rootProject.ext.versions.core
    desc = 'This library helps you to creates Speech Synthesis Markup Language (SSML) for your voice applications.'
    website = 'https://github.com/rewe-digital-incubator/dialog'
    licences = ['MIT']
    bintrayUser = project.properties['bintray.user']
    bintrayKey = project.properties['bintray.apikey']
    dryRun = false
    publications = ['ssmlBuilder']
}